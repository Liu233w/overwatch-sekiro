// reinhardt don't need a state machine

import "state-track.del";
import "block.del";
import "optimise.del";

rule: "initialize reinhardt"
Event.OngoingPlayer
Player.Reinhardt
{
    ContinueWhenServerLoadAcceptable();

    SetAbility1Enabled(EventPlayer(), true);
    SetAbility2Enabled(EventPlayer(), true);
    SetUltimateAbilityEnabled(EventPlayer(), false);
    AllowButton(EventPlayer(), Button.PrimaryFire);
    SetPrimaryFireEnabled(EventPlayer(), true);
    SetSecondaryFireEnabled(EventPlayer(), false);

    SetDamageDealt(EventPlayer(), 50);
}

rule: "set idle on alive"
Event.OngoingPlayer
Player.Reinhardt
if (IsAlive(EventPlayer()))
{
    OnIdle();
}

rule: "off idle when dead"
Event.OngoingPlayer
Player.Reinhardt
if (!IsAlive(EventPlayer()))
{
    OffIdle();
}

rule: "deal damage to player nearby when charging"
Event.OngoingPlayer
Player.Reinhardt
if (IsUsingAbility1(EventPlayer()))
{
    // do not start calculation immediately
    Wait(0.2, WaitBehavior.AbortWhenFalse);
    // this will trigger the damage dealt event
    while (true)
    {
        Damage(
            PlayersWithinRadius(PositionOf(EventPlayer()), 2, OppositeTeamOf(TeamOf(EventPlayer())), RadiusLOS.SurfacesAndEnemyBarriers),
            EventPlayer(),
            1
            );
        Wait(0.016, WaitBehavior.AbortWhenFalse);
    }
}

rule: "release shift button to stop charging"
Event.OngoingPlayer
Player.Reinhardt
if (!IsButtonHeld(EventPlayer(), Button.Ability1))
if (IsUsingAbility1(EventPlayer()))
{
    SetStatus(EventPlayer(), null, Status.Hacked, 0.1);

    // 2s cd if manually canceled
    SetPrimaryFireEnabled(EventPlayer(), false);
    SetAbility1Enabled(EventPlayer(), false);
    SetAbility2Enabled(EventPlayer(), false);

    Wait(2);

    SetPrimaryFireEnabled(EventPlayer(), true);
    SetAbility1Enabled(EventPlayer(), true);
    SetAbility2Enabled(EventPlayer(), true);
}

rule: "the trigger of e attack"
Event.OngoingPlayer
Player.Reinhardt
if (IsUsingAbility2(EventPlayer()))
{
    // only e ability stop reinhardt restore posture
    OffIdle();

    SetAtAbility2State(true);

    SetPrimaryFireEnabled(EventPlayer(), false);
    SetAbility1Enabled(EventPlayer(), false);
    SetAbility2Enabled(EventPlayer(), false);

    Wait(3);

    OnIdle();

    SetAtAbility2State(false);

    SetPrimaryFireEnabled(EventPlayer(), true);
    SetAbility1Enabled(EventPlayer(), true);
    SetAbility2Enabled(EventPlayer(), true);
}
