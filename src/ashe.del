// ashe don't need a state machine

import "state-track.del";
import "block.del";
import "optimise.del";
import "posture.del";

rule: "initialize ashe"
Event.OngoingPlayer
Player.Ashe
{
    ContinueWhenServerLoadAcceptable();

    SetPrimaryFireEnabled(EventPlayer(), true);
    SetAbility1Enabled(EventPlayer(), true);

    SetAbility2Enabled(EventPlayer(), false);
    SetUltimateAbilityEnabled(EventPlayer(), false);
    SetSecondaryFireEnabled(EventPlayer(), false);
}

rule: "set idle on alive"
Event.OngoingPlayer
Player.Ashe
if (IsAlive(EventPlayer()))
{
    AllowButton(EventPlayer(), Button.PrimaryFire);
    OnIdle(EventPlayer());
}

rule: "off idle when dead"
Event.OngoingPlayer
Player.Ashe
if (!IsAlive(EventPlayer()))
{
    OffIdle(EventPlayer());
}

rule: "set counter attack when using attack"
Event.OngoingPlayer
Player.Ashe
if (IsFiringPrimary(EventPlayer()))
if (IsAlive(EventPlayer()))
{
    // totally 0.5 sec counter attack time
    OffIdle(EventPlayer());
    // SetInCounterAttack(EventPlayer());
    DisallowButton(EventPlayer(), Button.PrimaryFire);
    Wait(0.5);
    AllowButton(EventPlayer(), Button.PrimaryFire);
    SetOffCounterAttack(EventPlayer());
    OnIdle(EventPlayer());
}

define playervar asheCanPush = true;

rule: "reset push variable when landed"
Event.OngoingPlayer
Player.Ashe
if (IsOnGround(EventPlayer()))
{
    asheCanPush = true;
}

rule: "ashe attacking player: add them to counter attack list"
Event.OnDamageDealt
if (Attacker() != null)
if (HeroOf(Attacker()) == Hero.Ashe)
if (!IsUsingAbility1(EventPlayer()))
{
    Heal(Victim(), null, EventDamage());
    AddCounterAttackPlayer(Attacker(), Victim());
}

rule: "ashe firing ability 1"
Event.OngoingPlayer
Player.Ashe
if (IsUsingAbility1(EventPlayer()))
if (IsAlive(EventPlayer()))
{
    OffIdle(EventPlayer());

    Wait(0.1, WaitBehavior.AbortWhenFalse);

    if (IsInAir(EventPlayer()) && asheCanPush)
    { // can fly when at air
        asheCanPush = false;
        define facingDirection = FacingDirectionOf(EventPlayer());
        // speed will be lower when player pointing at the ground, prevent player from flying too high.
        define speed = YOf(facingDirection) < -0.7 ? 15 : 20;
        ApplyImpulse(EventPlayer(), facingDirection * -1, speed, Relative.ToWorld, ContraryMotion.Cancel);
    }

    OnIdle(EventPlayer());
}

rule: "ashe use secondary attack as ability 1"
Event.OngoingPlayer
Player.Ashe
if (IsButtonHeld(EventPlayer(), Button.SecondaryFire))
{
    PressButton(EventPlayer(), Button.Ability1);
}