/*
The state machine of doomfist
*/

import "block.del";
import "debug.del";
import "posture.del";
import "optimise.del";

define playervar currentStateDoomfist = 0; // current state number in state machine
define playervar stateInitedDoomfist = false; // is state init code executed

rule: "Initialize doomfist" // if player switch a hero, the init code in main.del may not work
Event.OngoingPlayer
Player.Doomfist
{
    ContinueWhenServerLoadAcceptable();

    SetUltimateAbilityEnabled(EventPlayer(), false);
    DisallowButton(EventPlayer(), Button.PrimaryFire);

    SetDamageDealt(EventPlayer(), 100);
}

rule: "set state to 1 when spawned"
Event.OngoingPlayer
Player.Doomfist
if (IsAlive(EventPlayer()))
if (currentStateDoomfist == 0)
if (stateInitedDoomfist == false)
{
    ContinueWhenServerLoadAcceptable();
    currentStateDoomfist = 1;
}

rule: "set state to 0 when dead"
Event.OnDeath
Player.Doomfist
{
    currentStateDoomfist = 0;
    stateInitedDoomfist = false;
}

// ============== state initialize code ====================

rule: "state 1: idle"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 1)
if (stateInitedDoomfist == false)
{
    OnIdle(EventPlayer());

    AllowButton(EventPlayer(), Button.SecondaryFire);
    AllowButton(EventPlayer(), Button.Ability1);
    AllowButton(EventPlayer(), Button.Ability2);

    stateInitedDoomfist = true;
}

rule: "state 2: stunned"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 2)
if (stateInitedDoomfist == false)
{
    OffDeflecting(EventPlayer());
    OffBlocking(EventPlayer());
    stateInitedDoomfist = true;
}

rule: "state 11: deflecting"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 11)
if (stateInitedDoomfist == false)
{
    OnDeflecting(EventPlayer());
    stateInitedDoomfist = true;
}

rule: "state 12: blocking"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 12)
if (stateInitedDoomfist == false)
{
    OffDeflecting(EventPlayer());
    OnBlocking(EventPlayer());
    stateInitedDoomfist = true;
}

rule: "state 13: deflecting cooling down"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 13)
if (stateInitedDoomfist == false)
{
    stateInitedDoomfist = true;
}

rule: "state 14: blocking cooling down"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 14)
if (stateInitedDoomfist == false)
{
    OffBlocking(EventPlayer());
    stateInitedDoomfist = true;
}

rule: "state 21: fist charging"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 21)
if (stateInitedDoomfist == false)
{
    PlayEffect(AllPlayers(), PlayEffect.GoodExplosion, Color.Yellow, EventPlayer(), 2);
    StartHoldingButton(EventPlayer(), Button.SecondaryFire);
    stateInitedDoomfist = true;
}

rule: "state 22: fist releasing"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 22)
if (stateInitedDoomfist == false)
{
    StopHoldingButton(EventPlayer(), Button.SecondaryFire);
    stateInitedDoomfist = true;
}

rule: "state 23: fist cooling down"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 23)
if (stateInitedDoomfist == false)
{
    DisallowButton(EventPlayer(), Button.SecondaryFire);
    StopHoldingButton(EventPlayer(), Button.SecondaryFire);
    stateInitedDoomfist = true;
}

rule: "state 31: activating ability 2"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 31)
if (stateInitedDoomfist == false)
{
    stateInitedDoomfist = true;
}

rule: "state 32: ability 2 cooling down"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 32)
if (stateInitedDoomfist == false)
{
    DisallowButton(EventPlayer(), Button.Ability2);
    stateInitedDoomfist = true;
}

rule: "state 41: ability 1 activated"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 41)
if (stateInitedDoomfist == false)
{
    stateInitedDoomfist = true;
}

// ============== state switching rule =====================

rule: "1 -- crouch button --> 11"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 1)
if (IsButtonHeld(EventPlayer(), Button.Crouch))
if (stateInitedDoomfist == true)
{
    OffIdle(EventPlayer());
    DisallowButton(EventPlayer(), Button.Ability1);
    DisallowButton(EventPlayer(), Button.Ability2);
    DisallowButton(EventPlayer(), Button.SecondaryFire);
    StopHoldingButton(EventPlayer(), Button.SecondaryFire);
    currentStateDoomfist = 11;
    stateInitedDoomfist = false;
}

rule: "1 -- secondary fire --> 21"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 1)
if (IsFiringSecondary(EventPlayer()))
if (stateInitedDoomfist == true)
{
    OffIdle(EventPlayer());
    DisallowButton(EventPlayer(), Button.Ability1);
    DisallowButton(EventPlayer(), Button.Ability2);
    currentStateDoomfist = 21;
    stateInitedDoomfist = false;
}

rule: "1 -- using ability 1 --> 41"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 1)
if (IsUsingAbility1(EventPlayer()))
if (stateInitedDoomfist == true)
{
    DisallowButton(EventPlayer(), Button.Ability2);
    DisallowButton(EventPlayer(), Button.SecondaryFire);
    OffIdle(EventPlayer());
    currentStateDoomfist = 31;
    stateInitedDoomfist = false;
}

rule: "1 -- using ability 2 --> 31"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 1)
if (IsUsingAbility2(EventPlayer()))
if (stateInitedDoomfist == true)
{
    DisallowButton(EventPlayer(), Button.Ability1);
    DisallowButton(EventPlayer(), Button.SecondaryFire);
    OffIdle(EventPlayer());
    currentStateDoomfist = 31;
    stateInitedDoomfist = false;
}

rule: "any state -- deadly stunned --> 2"
Event.OngoingPlayer
Player.Doomfist
if (HasStatus(EventPlayer(), Status.Stunned))
if (IsDeadlyStunned(EventPlayer()))
if (stateInitedDoomfist == true)
{
    OffIdle(EventPlayer());
    currentStateDoomfist = 2;
    stateInitedDoomfist = false;
}

rule: "2 -- un-stunned --> 1"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 2)
if (!HasStatus(EventPlayer(), Status.Stunned))
if (stateInitedDoomfist == true)
{
    currentStateDoomfist = 1;
    stateInitedDoomfist = false;
}

rule: "11 -- 0.2 sec --> 12"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 11)
if (stateInitedDoomfist == true)
{
    Wait(0.2, WaitBehavior.AbortWhenFalse);
    currentStateDoomfist = 12;
    stateInitedDoomfist = false;
}

rule: "12 -- release crouch button --> 13"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 12)
if (!IsButtonHeld(EventPlayer(), Button.Crouch))
if (stateInitedDoomfist == true)
{
    currentStateDoomfist = 13;
    stateInitedDoomfist = false;
}

rule: "13 -- 0.2 sec --> 14"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 13)
if (stateInitedDoomfist == true)
{
    Wait(0.2, WaitBehavior.AbortWhenFalse);
    currentStateDoomfist = 14;
    stateInitedDoomfist = false;
}

rule: "13 -- crouch button --> 12"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 13)
if (IsButtonHeld(EventPlayer(), Button.Crouch))
if (stateInitedDoomfist == true)
{
    currentStateDoomfist = 12;
    stateInitedDoomfist = false;
}

rule: "14 -- 0.3 sec --> 1"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 14)
if (stateInitedDoomfist == true)
{
    Wait(0.3, WaitBehavior.AbortWhenFalse);
    currentStateDoomfist = 1;
    stateInitedDoomfist = false;
}

rule: "14 -- crouch button --> 11"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 14)
if (IsButtonHeld(EventPlayer(), Button.Crouch))
if (stateInitedDoomfist == true)
{
    currentStateDoomfist = 11;
    stateInitedDoomfist = false;
}

rule: "21 -- 0.2 sec --> 22"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 21)
if (stateInitedDoomfist == true)
{
    Wait(0.2, WaitBehavior.AbortWhenFalse);
    currentStateDoomfist = 22;
    stateInitedDoomfist = false;
}

rule: "21 -- release secondary fire --> 23"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 21)
if (!IsButtonHeld(EventPlayer(), Button.SecondaryFire))
if (stateInitedDoomfist == true)
{
    currentStateDoomfist = 23;
    stateInitedDoomfist = false;
}

rule: "21 -- left button --> 23"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 21)
if (IsButtonHeld(EventPlayer(), Button.PrimaryFire))
if (stateInitedDoomfist == true)
{
    currentStateDoomfist = 23;
    stateInitedDoomfist = false;
}

rule: "21 -- crouch button --> 11"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 21)
if (IsButtonHeld(EventPlayer(), Button.Crouch))
if (stateInitedDoomfist == true)
{
    currentStateDoomfist = 11;
    stateInitedDoomfist = false;
}

rule: "22 -- 0.3 sec --> 23"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 22)
if (stateInitedDoomfist == true)
{
    Wait(0.3, WaitBehavior.AbortWhenFalse);
    currentStateDoomfist = 23;
    stateInitedDoomfist = false;
}

rule: "23 -- 0.5 sec --> 1"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 23)
if (stateInitedDoomfist == true)
{
    Wait(0.5, WaitBehavior.AbortWhenFalse);
    currentStateDoomfist = 1;
    stateInitedDoomfist = false;
}

rule: "23 -- crouch button --> 12"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 23)
if (IsButtonHeld(EventPlayer(), Button.Crouch))
if (stateInitedDoomfist == true)
{
    currentStateDoomfist = 12;
    stateInitedDoomfist = false;
}

rule: "31 -- finish ability 2 --> 32"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 31)
if (!IsUsingAbility2(EventPlayer()))
if (stateInitedDoomfist == true)
{
    currentStateDoomfist = 32;
    stateInitedDoomfist = false;
}

rule: "32 -- 0.3 sec --> 1"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 32)
if (stateInitedDoomfist == true)
{
    Wait(0.5, WaitBehavior.AbortWhenFalse);
    currentStateDoomfist = 1;
    stateInitedDoomfist = false;
}

rule: "41 -- 1 sec --> 1"
Event.OngoingPlayer
Player.Doomfist
if (currentStateDoomfist == 41)
if (stateInitedDoomfist == true)
{
    Wait(1, WaitBehavior.AbortWhenFalse);
    currentStateDoomfist = 1;
    stateInitedDoomfist = false;
}

// =============== interact as crouch =======================
rule: "interact to crouch"
Event.OngoingPlayer
Player.Doomfist
if (IsButtonHeld(EventPlayer(), Button.Interact))
{
    StartHoldingButton(EventPlayer(), Button.Crouch);
}

rule: "un-interact to un-crouch"
Event.OngoingPlayer
Player.Doomfist
if (!IsButtonHeld(EventPlayer(), Button.Interact))
{
    StopHoldingButton(EventPlayer(), Button.Crouch);
}

// =============== other rule ===============================

rule: "disable passive"
Event.OngoingPlayer
Player.Doomfist
if (MaxHealth(EventPlayer()) > 250)
{
    Damage(EventPlayer(), null, MaxHealth() - 250);
}

// ================= debugging ====================

define playervar debugHudDoomfist;

rule: "debug doomfist"
Event.OngoingPlayer
Player.Doomfist
if (IsDebugging())
{
    if (debugHudDoomfist == 0)
    {
        CreateHudText(EventPlayer(), <"status: <0> - initial: <1>", currentStateDoomfist, stateInitedDoomfist>);
        debugHudDoomfist = LastTextID();
    }
}