/*
code about attack
*/

import "block.del";
import "posture.del";
import "state-track.del";

// to prevent from frequently allocating and destroying arrays
// which can lead to performance issues
define playervar attackingVictimList = [];
define playervar attackingDirectDamageList = [];
define playervar attackingHittedPostureList = [];
define playervar attackingDeflectedPostureList = [];
define playervar attackingListSize = 0;

rule: "add victim to list after attacking, if the victim is not blocking"
Event.OnDamageDealt
if (Attacker() != null)
if (!IsDeadlyStunned(Victim()))
if (!IsBlocking(Victim()) && !IsDeflecting(Victim()))
if (HeroOf(Attacker()) != Hero.Ashe || IsUsingAbility1(Attacker()))
{
    if (HeroOf(Attacker()) == Hero.Doomfist && IsUsingAbility1(Attacker()))
    {
        // doomfist's ability 1: deal no damage
        Heal(Victim(), null, EventDamage());
        if (AltitudeOf(Victim()) > 1)
        {
            // deal 150 posture damage if not on the ground
            GainPosture(Victim(), 150, Attacker());
        }
    }
    else if (HeroOf(Attacker()) == Hero.Genji && IsUsingAbility1(Attacker()))
    {
        // genji's ability 1: deal no damage or posture
        Heal(Victim(), null, EventDamage());
    }
    else if (HeroOf(Attacker()) == Hero.Reinhardt && AtAbility2State(Attacker()))
    {
        // reinhardt's ability 2: deal no damage, but 200 posture when hitted.
        Heal(Victim(), null, EventDamage());

        attackingVictimList[attackingListSize] = Victim();
        attackingDirectDamageList[attackingListSize] = 0;
        attackingHittedPostureList[attackingListSize] = 200;
        attackingDeflectedPostureList[attackingListSize] = 0;
        attackingListSize += 1;
    }
    else if (HeroOf(Attacker()) == Hero.Reinhardt && IsUsingAbility1(Attacker()))
    {
        // reinhardt's ability 1: deal no damage, but 200 posture when hitted.
        // if deflected, gain 200 posture damage
        Heal(Victim(), null, EventDamage());

        attackingVictimList[attackingListSize] = Victim();
        attackingDirectDamageList[attackingListSize] = 0;
        attackingHittedPostureList[attackingListSize] = 200;
        attackingDeflectedPostureList[attackingListSize] = 200;
        attackingListSize += 1;

        // stop reinhardt
        SetStatus(Attacker(), null, Status.KnockedDown, 0.5);
    }
    else if (HeroOf(Attacker()) == Hero.Mercy)
    {
        attackingVictimList[attackingListSize] = Victim();
        attackingDirectDamageList[attackingListSize] = EventDamage();
        // mercy deal extra 50 posture damage if victim's posture reaches 150
        if (Posture(Victim()) >= 150)
        {
            attackingHittedPostureList[attackingListSize] = 50 - EventDamage();
        }
        else
        {
            attackingHittedPostureList[attackingListSize] = 0;
        }
        // mercy receive 150 posture damage if deflected
        attackingDeflectedPostureList[attackingListSize] = 150 - EventDamage();
        attackingListSize += 1;
    }
    else if (HeroOf(Victim()) == Hero.Mercy)
    {
        attackingVictimList[attackingListSize] = Victim();
        attackingDirectDamageList[attackingListSize] = EventDamage();
        attackingHittedPostureList[attackingListSize] = 0;
        // mercy will set attacker's posture to 150 if it's not reached 150
        if (Posture(Victim()) < 150)
        {
            attackingDeflectedPostureList[attackingListSize] = 150 - Posture(Victim()) - EventDamage();
        }
        else
        {
            attackingDeflectedPostureList[attackingListSize] = -EventDamage();
        }
        attackingListSize += 1;
    }
    else if (HeroOf(Attacker()) == Hero.Doomfist && IsUsingAbility2(Attacker()))
    {
        // doomfist's ability 2: deal 1/2 damage
        Heal(Victim(), null, EventDamage() / 2);

        attackingVictimList[attackingListSize] = Victim();
        attackingDirectDamageList[attackingListSize] = EventDamage() / 2;
        attackingHittedPostureList[attackingListSize] = 0;
        attackingDeflectedPostureList[attackingListSize] = 0;
        attackingListSize += 1;
    }
    else
    {
        attackingVictimList[attackingListSize] = Victim();
        attackingDirectDamageList[attackingListSize] = EventDamage();
        attackingHittedPostureList[attackingListSize] = 0;
        attackingDeflectedPostureList[attackingListSize] = 0;
        attackingListSize += 1;
    }
}

rule: "gain posture if victim is deflecting"
Event.OnDamageDealt
if (Attacker() != null)
if (!IsDeadlyStunned(Victim()))
if (IsDeflecting(Victim()))
if (HeroOf(Attacker()) != Hero.Ashe || IsUsingAbility1(Attacker()))
{
    if (HeroOf(Attacker()) == Hero.Doomfist && IsUsingAbility1(Attacker()))
    {
        // doomfist's ability 1: deal no damage
        Heal(Victim(), null, EventDamage());
    }
    else if (HeroOf(Attacker()) == Hero.Genji && IsUsingAbility1(Attacker()))
    {
        // genji's ability 1: cannot be deflected
        Heal(Victim(), null, EventDamage());
    }
    else if (HeroOf(Attacker()) == Hero.Reinhardt && AtAbility2State(Attacker()))
    {
        // reinhardt's ability 2: do nothing
        Heal(Victim(), null, EventDamage());
    }
    else if (HeroOf(Attacker()) == Hero.Reinhardt && IsUsingAbility1(Attacker()))
    {
        // reinhardt's ability 1: gain 200 posture damage
        Heal(Victim(), null, EventDamage());
        GainPosture(Attacker(), 200, Victim()); // it can stun attack immediately
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);

        OnSucceedDeflecting(Victim());
    }
    else if (HeroOf(Attacker()) == Hero.Mercy)
    {
        // mercy cannot be deflected
        Heal(Victim(), null, EventDamage());
    }
    else if (HeroOf(Victim()) == Hero.Mercy)
    {
        // mercy will set attacker's posture to 150 if it's not reached 150
        Heal(Victim(), null, EventDamage());
        if (Posture(Attacker()) < 150)
        {
            GainPosture(Attacker(), 150 - Posture(Attacker()), Victim());
        }
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);

        OnSucceedDeflecting(Victim());
    }
    else
    {
        Heal(Victim(), null, EventDamage());
        GainPosture(Attacker(), EventDamage(), Victim());
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);

        OnSucceedDeflecting(Victim());
    }
}

rule: "deal posture if victim is blocking"
Event.OnDamageDealt
if (Attacker() != null)
if (!IsDeadlyStunned(Victim()))
if (IsBlocking(Victim()) && !IsDeflecting(Victim()))
if (HeroOf(Attacker()) != Hero.Ashe || IsUsingAbility1(Attacker()))
{
    if (HeroOf(Attacker()) == Hero.Doomfist && IsUsingAbility2(Attacker()))
    {
        // doomfist's ability 2: deal 2X posture damage
        Heal(Victim(), null, EventDamage());
        GainPosture(Victim(), EventDamage() * 2, Attacker());
    }
    else if (HeroOf(Attacker()) == Hero.Doomfist && IsUsingAbility1(Attacker()))
    {
        // doomfist's ability 1: deal no damage
        Heal(Victim(), null, EventDamage());
    }
    else if (HeroOf(Attacker()) == Hero.Genji && IsUsingAbility1(Attacker()))
    {
        // genji's ability 1: deal no posture damage
        Heal(Victim(), null, EventDamage());
    }
    else if (HeroOf(Attacker()) == Hero.Reinhardt && AtAbility2State(Attacker()))
    {
        // reinhardt's ability 2: do nothing
        Heal(Victim(), null, EventDamage());
    }
    else if (HeroOf(Attacker()) == Hero.Reinhardt && IsUsingAbility1(Attacker()))
    {
        // reinhardt's ability 1: knockdown reinhardt, deal reinhardt posture
        Heal(Victim(), null, EventDamage());
        GainPosture(Attacker(), 150, Victim());
        SetStatus(Attacker(), Victim(), Status.KnockedDown, 2);
    }
    else
    {
        Heal(Victim(), null, EventDamage());
        GainPosture(Victim(), EventDamage() / 2, Attacker());
    }
}

rule: "start checking victim state after a short delay"
Event.OngoingPlayer
if (attackingListSize > 0)
{
    if (HeroOf(EventPlayer()) == Hero.Doomfist)
    {
        Wait(0.15, WaitBehavior.AbortWhenFalse);
    }
    else
    {
        Wait(0.1, WaitBehavior.AbortWhenFalse);
    }

    for (define i = 0; i < attackingListSize; i++)
    {
        if (InCounterAttack(attackingVictimList[i]) && IsCounterAttacked(attackingVictimList[i], EventPlayer()))
        {
            SetPosture(attackingVictimList[i], 0);
            Heal(attackingVictimList[i], null, 150 + attackingDirectDamageList[i]);
            // that can stun the attacker
            GainPosture(EventPlayer(), 300, attackingVictimList[i]);
        }
        else if (IsDeflecting(attackingVictimList[i]) || IsBlocking(attackingVictimList[i]))
        {
            // deflecting
            Heal(attackingVictimList[i], null, attackingDirectDamageList[i]);
            GainPosture(EventPlayer(), attackingDirectDamageList[i] + attackingDeflectedPostureList[i], attackingVictimList[i]);
            PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, attackingVictimList[i], 100);
            PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, attackingVictimList[i], 100);
            PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, attackingVictimList[i], 100);
            PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, attackingVictimList[i], 100);

            OnSucceedDeflecting(attackingVictimList[i]);
        }
        else
        {
            GainPosture(attackingVictimList[i], attackingDirectDamageList[i] + attackingHittedPostureList[i], EventPlayer());
        }
    }

    attackingListSize = 0;
}