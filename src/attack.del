/*
code about attack
*/

import "block.del";
import "posture.del";
import "state-track.del";

class AttackingEvent
{
    public define victim;

    public define directDamage;

    // TODO: let each kind of attacking have their own posture damage
    // can be achieved by adding a multiplier (* the multiplier when hitted in blocking)

    public define extraPostureDamageWhenDirectlyHitted = 0;
    public define extraPostureDamageWhenDeflected = 0;

    public AttackingEvent(define victim, define damage)
    {
        this.victim = victim;
        this.directDamage = damage;
    }
}

AttackingEvent playervar attacking = [];

rule: "add victim to list after attacking, if the victim is not blocking"
Event.OnDamageDealt
if (Attacker() != null)
if (HeroOf(Attacker()) != Hero.Ashe)
if (HeroOf(Victim()) != Hero.Ashe)
if (!IsBlocking(Victim()) && !IsDeflecting(Victim()))
{
    if (HeroOf(Attacker()) == Hero.Doomfist && IsUsingAbility2(Attacker()))
    {
        // doomfist's ability 2: deal 1/2 damage
        Heal(Victim(), null, EventDamage() / 2);
        attacking = Append(attacking, new AttackingEvent(Victim(), EventDamage() / 2));
    }
    else if (HeroOf(Attacker()) == Hero.Doomfist && IsUsingAbility1(Attacker()))
    {
        // doomfist's ability 1: deal no damage
        Heal(Victim(), null, EventDamage());
        if (AltitudeOf(Victim()) > 1)
        {
            // deal 150 posture damage if not on the ground
            GainPosture(Victim(), 150, Attacker());
        }
    }
    else if (HeroOf(Attacker()) == Hero.Genji && IsUsingAbility1(Attacker()))
    {
        // genji's ability 1: deal no damage or posture
        Heal(Victim(), null, EventDamage());
    }
    else if (HeroOf(Attacker()) == Hero.Genji && HeroOf(Victim()) == Hero.Roadhog)
    {
        // only deal 1/2 damage to commander
        if (EventDamage() > 60)
        {
            Heal(Victim(), null, EventDamage() - 60);
        }
        GainPosture(Victim(), 60, Attacker());
    }
    else if (HeroOf(Attacker()) == Hero.Reinhardt && AtAbility2State(Attacker()))
    {
        // reinhardt's ability 2: deal no damage, but 200 posture when hitted.
        Heal(Victim(), null, EventDamage());
        AttackingEvent e = new AttackingEvent(Victim(), 0);
        e.extraPostureDamageWhenDirectlyHitted = 200;
        attacking = Append(attacking, e);
    }
    else if (HeroOf(Attacker()) == Hero.Reinhardt && IsUsingAbility1(Attacker()))
    {
        // reinhardt's ability 1: deal no damage, but 200 posture when hitted.
        // if deflected, gain 200 posture damage
        Heal(Victim(), null, EventDamage());
        AttackingEvent e = new AttackingEvent(Victim(), 0);
        e.extraPostureDamageWhenDirectlyHitted = 200;
        e.extraPostureDamageWhenDeflected = 200;
        attacking = Append(attacking, e);

        // stop reinhardt
        SetStatus(Attacker(), null, Status.KnockedDown, 0.5);
    }
    else if (HeroOf(Attacker()) == Hero.Mercy)
    {
        // mercy deal no damage or posture when directly attacking
        Heal(Victim(), null, EventDamage());
    }
    else if (HeroOf(Victim()) == Hero.Mercy)
    {
        // mercy deal fixed 150 posture damage when deflecting
        AttackingEvent e = new AttackingEvent(Victim(), EventDamage());
        e.extraPostureDamageWhenDeflected = 150 - EventDamage();
        attacking = Append(attacking, e);
    }
    else
    {
        attacking = Append(attacking, new AttackingEvent(Victim(), EventDamage()));
    }
}

rule: "gain posture if victim is deflecting"
Event.OnDamageDealt
if (Attacker() != null)
if (HeroOf(Attacker()) != Hero.Ashe)
if (HeroOf(Victim()) != Hero.Ashe)
if (IsDeflecting(Victim()))
{
    if (HeroOf(Attacker()) == Hero.Doomfist && IsUsingAbility1(Attacker()))
    {
        // doomfist's ability 1: deal no damage
        Heal(Victim(), null, EventDamage());
    }
    else if (HeroOf(Attacker()) == Hero.Genji && IsUsingAbility1(Attacker()))
    {
        // genji's ability 1: cannot be deflected
        Heal(Victim(), null, EventDamage());
    }
    else if (HeroOf(Attacker()) == Hero.Reinhardt && AtAbility2State(Attacker()))
    {
        // reinhardt's ability 2: do nothing
        Heal(Victim(), null, EventDamage());
    }
    else if (HeroOf(Attacker()) == Hero.Reinhardt && IsUsingAbility1(Attacker()))
    {
        // reinhardt's ability 1: gain 200 posture damage
        Heal(Victim(), null, EventDamage());
        GainPosture(Attacker(), 200, Victim()); // it can stun attack immediately
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);

        OnSucceedDeflecting(Victim());
    }
    else if (HeroOf(Attacker()) == Hero.Mercy)
    {
        // mercy cannot be deflected
        Heal(Victim(), null, EventDamage());
    }
    else if (HeroOf(Victim()) == Hero.Mercy)
    {
        // mercy deal fixed 150 posture damage when deflecting
        Heal(Victim(), null, EventDamage());
        GainPosture(Attacker(), 150, Victim()); // it can stun attack immediately
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);

        OnSucceedDeflecting(Victim());
    }
    else
    {
        Heal(Victim(), null, EventDamage());
        GainPosture(Attacker(), EventDamage(), Victim());
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);
        PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, Victim(), 100);

        OnSucceedDeflecting(Victim());
    }
}

rule: "deal posture if victim is blocking"
Event.OnDamageDealt
if (Attacker() != null)
if (HeroOf(Attacker()) != Hero.Ashe)
if (HeroOf(Victim()) != Hero.Ashe)
if (IsBlocking(Victim()) && !IsDeflecting(Victim()))
{
    if (HeroOf(Attacker()) == Hero.Doomfist && IsUsingAbility2(Attacker()))
    {
        // doomfist's ability 2: deal 2X posture damage
        Heal(Victim(), null, EventDamage());
        GainPosture(Victim(), EventDamage() * 2, Attacker());
    }
    else if (HeroOf(Attacker()) == Hero.Doomfist && IsUsingAbility1(Attacker()))
    {
        // doomfist's ability 1: deal no damage
        Heal(Victim(), null, EventDamage());
    }
    else if (HeroOf(Attacker()) == Hero.Genji && IsUsingAbility1(Attacker()))
    {
        // genji's ability 1: deal no posture damage
        Heal(Victim(), null, EventDamage());
    }
    else if (HeroOf(Attacker()) == Hero.Reinhardt && AtAbility2State(Attacker()))
    {
        // reinhardt's ability 2: do nothing
        Heal(Victim(), null, EventDamage());
    }
    else if (HeroOf(Attacker()) == Hero.Reinhardt && IsUsingAbility1(Attacker()))
    {
        // reinhardt's ability 1: knockdown reinhardt, deal reinhardt posture
        Heal(Victim(), null, EventDamage());
        GainPosture(Attacker(), 150, Victim());
        SetStatus(Attacker(), Victim(), Status.KnockedDown, 2);
    }
    else if (HeroOf(Attacker()) == Hero.Mercy)
    {
        // mercy deal no damage
        Heal(Victim(), null, EventDamage());
    }
    else
    {
        Heal(Victim(), null, EventDamage());
        GainPosture(Victim(), EventDamage() / 2, Attacker());
    }
}

rule: "start checking victim state after a short delay"
Event.OngoingPlayer
if (CountOf(attacking) > 0)
{
    if (HeroOf(EventPlayer()) == Hero.Doomfist)
    {
        Wait(0.15, WaitBehavior.AbortWhenFalse);
    }
    else
    {
        Wait(0.1, WaitBehavior.AbortWhenFalse);
    }

    // use another variable to prevent memory leak
    define attackingList = attacking;
    attacking = [];

    foreach (AttackingEvent e in attackingList)
    {
        if (IsDeflecting(e.victim) || IsBlocking(e.victim))
        {
            // deflecting
            Heal(e.victim, null, e.directDamage);
            GainPosture(EventPlayer(), e.directDamage + e.extraPostureDamageWhenDeflected, e.victim);
            PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, e.victim, 100);
            PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, e.victim, 100);
            PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, e.victim, 100);
            PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, e.victim, 100);

            OnSucceedDeflecting(e.victim);
        }
        else
        {
            GainPosture(e.victim, e.directDamage + e.extraPostureDamageWhenDirectlyHitted, EventPlayer());
        }

        delete(e);
    }

    Loop();
}