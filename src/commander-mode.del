import "block.del";
import "settings.del";
import "posture.del";

rule: "disable score rule"
{
    DisableAnnouncer();
    DisableScoring();
}

define globalvar team1FlagSpawn = null;
define globalvar team2FlagSpawn = null;

rule: "set flag spawn position at the beginning"
if (IsInSetup())
{
    Wait(3);
    team1FlagSpawn = FlagPosition(Team.Team1);
    team2FlagSpawn = FlagPosition(Team.Team2);
}

rule: "spawn commander at flag spawn"
if (IsGameInProgress())
{
    SetObjectiveDescription(AllPlayers(), <"kill enemy <0> -> score", Hero.Roadhog>);
    // for performance issues
    Wait(1);
    CreateDummyBot(Hero.Roadhog, Team.Team1, 5, team1FlagSpawn, Vector(0, 0, 0));
    Wait(1);
    CreateDummyBot(Hero.Roadhog, Team.Team2, 5, team2FlagSpawn, Vector(0, 0, 0));
}

rule: "commander config"
Event.OngoingPlayer
Player.Roadhog
{
    SetRespawnMaxTime(EventPlayer(), 30);
    SetMaxPosture(EventPlayer(), 1200);

    CreateHudText(AllPlayers(TeamOf(EventPlayer())), <"teammate: defend <0>%", Posture(EventPlayer())/12>, null, null, 
        Location.Top, 1, Color.White, Color.White, Color.White, StringRev.VisibleToAndString);
    CreateHudText(AllPlayers(OppositeTeamOf(TeamOf(EventPlayer()))), <"enemy: defend <0>%", Posture(EventPlayer())/12>, null, null, 
        Location.Top, 1, Color.White, Color.White, Color.White, StringRev.VisibleToAndString);

    OnBlocking(EventPlayer());
}

rule: "off blocking when stunned or dead"
Event.OngoingPlayer
Player.Roadhog
if (IsDeadlyStunned(EventPlayer()) || !IsAlive(EventPlayer()))
{
    OffBlocking(EventPlayer());
}

rule: "on blocking when not stunned"
Event.OngoingPlayer
Player.Roadhog
if (!IsDeadlyStunned(EventPlayer()))
{
    OnBlocking(EventPlayer());
}

define playervar myTeamFlagSpawn;
rule: "set my team flag spawn location"
Event.OngoingPlayer
Player.Roadhog
Team.Team1
{
    myTeamFlagSpawn = team1FlagSpawn;
}

rule: "set my team flag spawn location"
Event.OngoingPlayer
Player.Roadhog
Team.Team2
{
    myTeamFlagSpawn = team2FlagSpawn;
}

rule: "kill hostile commander to score"
Event.OnDeath
Player.Roadhog
{
    ModifyTeamScore(OppositeTeamOf(TeamOf(EventPlayer())), 1);
    BigMessage(AllPlayers(TeamOf(EventPlayer())), <"teammate <0> spawn: <1> sec", HeroIconString(Hero.Roadhog), 30>);
    BigMessage(AllPlayers(OppositeTeamOf(TeamOf(EventPlayer()))), <"enemy <0> spawn: <1> sec", HeroIconString(Hero.Roadhog), 30>);
}

rule: "teleport commander after spawned"
Event.OngoingPlayer
Player.Roadhog
if (IsAlive(EventPlayer()))
{
    Teleport(EventPlayer(), myTeamFlagSpawn);
    BigMessage(AllPlayers(TeamOf(EventPlayer())), <"teammate <0> spawned", HeroIconString(Hero.Roadhog)>);
    BigMessage(AllPlayers(OppositeTeamOf(TeamOf(EventPlayer()))), <"enemy <0> spawned", HeroIconString(Hero.Roadhog)>);
}

rule: "prevent commander from moving"
Event.OngoingPlayer
Player.Roadhog
if (IsAlive(EventPlayer()))
if (IsInAir(EventPlayer()))
{
    Teleport(EventPlayer(), myTeamFlagSpawn);
}