import "block.del";
import "settings.del";
import "posture.del";

rule: "disable win rule"
{
    DisableCompletion();
    DisableAnnouncer();
}

rule: "only slot 0 and 5 is allowed to choose roadhog"
Event.OnPlayerJoin
Player.Slot0
{
    SetHeroRoster(EventPlayer(), Hero.Roadhog);
}

rule: "only slot 0 and 5 is allowed to choose roadhog"
Event.OnPlayerJoin
Player.Slot5
{
    SetHeroRoster(EventPlayer(), Hero.Roadhog);
}

define globalvar final = false;

rule: "not final at the beginning, then set final after 3 minutes"
if (IsGameInProgress())
if (!final)
{
    final = false;
    Wait(60 * 2.5);
    final = true;
    BigMessage(AllPlayers(), "final_phase");
}

rule: "if roadhog is close enough, set final phrase immediately"
if (IsGameInProgress())
if (DistanceBetween(PlayersOnHero(Hero.Roadhog, Team.Team1)[0], PlayersOnHero(Hero.Roadhog, Team.Team2)[0]) <= 20)
{
    final = true;
    BigMessage(AllPlayers(), "final_phase");
}

rule: "commander remove all abilities and set max health"
Event.OngoingPlayer
Player.Roadhog
if (IsInSetup())
{
    final = false;

    SetPrimaryFireEnabled(EventPlayer(), false);
    SetSecondaryFireEnabled(EventPlayer(), false);
    SetAbility1Enabled(EventPlayer(), false);
    SetAbility2Enabled(EventPlayer(), false);
    SetUltimateAbilityEnabled(EventPlayer(), false);

    SetMaxHealth(EventPlayer(), 400);
    SetMaxPosture(EventPlayer(), 2400);

    SetDamageDealt(EventPlayer(), 800);

    SetMoveSpeed(EventPlayer(), 15);

    OnIdle(EventPlayer());
}

rule: "commander be quick in final, and make health less"
Event.OngoingPlayer
Player.Roadhog
if (final)
{
    ClearStatus(EventPlayer(), Status.PhasedOut);
    ClearStatus(EventPlayer(), Status.Asleep);
    ClearStatus(EventPlayer(), Status.Rooted);
    SetStatus(EventPlayer(), null, Status.Hacked, 0.1);

    MinWait();

    SetHealth(EventPlayer(), Health(EventPlayer()) * 1/4);
    SetMaxHealth(EventPlayer(), 100);
    SetMoveSpeed(EventPlayer(), 200);

    SetPrimaryFireEnabled(EventPlayer(), true);
    SetDamageDealt(EventPlayer(), 100);

    // commander is stuck
    if (DistanceBetween(EventPlayer(), PlayersOnHero(Hero.Roadhog, OppositeTeamOf(TeamOf(EventPlayer())))) > 30)
    {
        Teleport(EventPlayer(), ObjectivePosition(ObjectiveIndex()));
    }
}

rule: "kill hostile commander to win"
Event.OnDeath
Player.Roadhog
{
    DeclareRoundVictory(OppositeTeamOf(TeamOf(EventPlayer())));
}

rule: "show commander's location"
Event.OngoingPlayer
if (IsGameInProgress())
{
    define friendlyCommander = PlayersOnHero(Hero.Roadhog, TeamOf(EventPlayer()))[0];
    define enemyCommander = PlayersOnHero(Hero.Roadhog,OppositeTeamOf(TeamOf(EventPlayer())))[0];
    CreateIcon(EventPlayer(), friendlyCommander, Icon.Heart, IconRev.VisibleToAndPosition, Color.Blue, true);
    CreateIcon(EventPlayer(), enemyCommander, Icon.Skull, IconRev.VisibleToAndPosition, Color.Red, true);
}

rule: "show objective health and posture"
Event.OngoingPlayer
{
    CreateHudText(EventPlayer(), <"teammate: life <0>", 
        Health(PlayersOnHero(Hero.Roadhog, TeamOf(EventPlayer()))[0])>);
    CreateHudText(EventPlayer(), <"enemy: life <0>", 
        Health(PlayersOnHero(Hero.Roadhog, OppositeTeamOf(TeamOf(EventPlayer())))[0])>);
}

rule: "if current posture > 1/2, commander stop moving and use ultimate 10 sec, then sleep commander"
Event.OngoingPlayer
Player.Roadhog
if (Posture(EventPlayer()) > 1200 && !final)
{
    SetStatus(EventPlayer(), null, Status.Rooted, 10);
    SetUltimateAbilityEnabled(EventPlayer(), true);
    MinWait();
    SetUltimateCharge(EventPlayer(), 100);
    MinWait();
    PressButton(EventPlayer(), Button.Ultimate);
    
    BigMessage(AllPlayers(), <"<0> use_ultimate_ability", HeroIconString(Hero.Roadhog)>);

    Wait(10);

    SetPosture(EventPlayer(), 0);
    SetUltimateAbilityEnabled(EventPlayer(), false);
    SetStatus(EventPlayer(), null, Status.Asleep, 20);
    SetStatus(EventPlayer(), null, Status.PhasedOut, 20);
}

rule: "prevent commander from dead by cliff or stuck"
Event.OngoingPlayer
Player.Roadhog
if (IsGameInProgress())
// if (PositionOf(EventPlayer()) != NearestWalkablePosition(PositionOf(EventPlayer())))
if ((!IsMoving(EventPlayer()) && !final) || IsInAir(EventPlayer()))
{
    Teleport(EventPlayer(), NearestWalkablePosition(PositionOf(EventPlayer()) + Vector(RandomReal(0.5, 3), RandomReal(0.5, 3), RandomReal(0.5, 3))));
}

rule: "prevent commander from healed by mercy"
Event.OnHealingTaken
Player.Roadhog
if (HeroOf(Healer()) == Hero.Mercy)
{
    Damage(EventPlayer(), null, EventHealing());
}

define playervar movingHud;

rule: "debug: show if commander moving"
Event.OngoingPlayer
Player.Roadhog
if (IsMoving(EventPlayer()))
{
    CreateHudText(EventPlayer(), "location");
    movingHud = LastTextID();
}

rule: "debug: un-show if commander not moving"
Event.OngoingPlayer
Player.Roadhog
if (!IsMoving(EventPlayer()))
{
    if (movingHud != 0)
    {
        DestroyHudText(movingHud);
    }
}