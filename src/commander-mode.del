import "block.del";
import "settings.del";
import "posture.del";

rule: "disable win rule"
{
    DisableCompletion();
    DisableAnnouncer();
}

rule: "only slot 0 and 5 is allowed to choose roadhog"
Event.OnPlayerJoin
Player.Slot0
{
    SetHeroRoster(EventPlayer(), Hero.Roadhog);
}

rule: "only slot 0 and 5 is allowed to choose roadhog"
Event.OnPlayerJoin
Player.Slot5
{
    SetHeroRoster(EventPlayer(), Hero.Roadhog);
}

define globalvar final = false;

rule: "not final at the beginning"
if (IsGameInProgress())
{
    final = false;
}

rule: "when commander reach object point, set final"
Event.OngoingPlayer
Player.Roadhog
if (IsOnObjective(EventPlayer()))
{
    final = true;
}

rule: "commander remove all abilities and set max health"
Event.OngoingPlayer
Player.Roadhog
{
    SetPrimaryFireEnabled(EventPlayer(), false);
    SetSecondaryFireEnabled(EventPlayer(), false);
    SetAbility1Enabled(EventPlayer(), false);
    SetAbility2Enabled(EventPlayer(), false);
    SetUltimateAbilityEnabled(EventPlayer(), false);

    SetMaxHealth(EventPlayer(), 600);
    SetMaxPosture(EventPlayer(), 5000); // need 25 times charges of reinhardt

    OnIdle(EventPlayer());
}

rule: "commander be slow when not in final"
Event.OngoingPlayer
Player.Roadhog
if (!final)
{
    SetMoveSpeed(EventPlayer(), 15);
}

rule: "commander be quick in final, and make health less"
Event.OngoingPlayer
Player.Roadhog
if (final)
{
    ClearStatus(EventPlayer(), Status.PhasedOut);
    ClearStatus(EventPlayer(), Status.Asleep);

    MinWait();

    SetHealth(EventPlayer(), Health(EventPlayer()) * 1/4);
    SetMoveSpeed(EventPlayer(), 200);

    // commander sticked
    if (IsInSpawnRoom(EventPlayer()))
    {
        Teleport(EventPlayer(), ObjectivePosition());
    }
}

rule: "kill hostile commander to win"
Event.OnDeath
Player.Roadhog
{
    DeclareRoundVictory(OppositeTeamOf(TeamOf(EventPlayer())));
}

rule: "show commander's location"
Event.OngoingPlayer
if (IsGameInProgress())
{
    define friendlyCommander = PlayersOnHero(Hero.Roadhog, TeamOf(EventPlayer()))[0];
    define enemyCommander = PlayersOnHero(Hero.Roadhog,OppositeTeamOf(TeamOf(EventPlayer())))[0];
    CreateIcon(EventPlayer(), friendlyCommander, Icon.Heart, IconRev.VisibleToAndPosition, Color.Blue, true);
    CreateIcon(EventPlayer(), enemyCommander, Icon.Skull, IconRev.VisibleToAndPosition, Color.Red, true);
}

rule: "show objective health and posture"
Event.OngoingPlayer
{
    CreateHudText(EventPlayer(), <"teammate: life <0>", 
        Health(PlayersOnHero(Hero.Roadhog, TeamOf(EventPlayer()))[0])>);
    CreateHudText(EventPlayer(), <"enemy: life <0>", 
        Health(PlayersOnHero(Hero.Roadhog, OppositeTeamOf(TeamOf(EventPlayer())))[0])>);
}

rule: "if current posture > 600, sleep commander and make it phased out"
Event.OngoingPlayer
Player.Roadhog
if (Posture(EventPlayer()) > 600 && !final)
{
    SetStatus(EventPlayer(), null, Status.Asleep, 30);
    SetStatus(EventPlayer(), null, Status.PhasedOut, 30);
    
    BigMessage(AllPlayers(), <"<0> sleep <1> sec", HeroIconString(Hero.Roadhog), 30>);
}

rule: "prevent commander from dead by cliff"
Event.OngoingPlayer
Player.Roadhog
if (IsInAir(EventPlayer()))
{
    Teleport(EventPlayer(), NearestWalkablePosition(PositionOf(EventPlayer())));
}

rule: "prevent commander from healed by mercy"
Event.OnHealingTaken
Player.Roadhog
if (HeroOf(Healer()) == Hero.Mercy)
{
    Damage(EventPlayer(), null, EventHealing());
}